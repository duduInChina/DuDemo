boolean toapp = moduleToApplication.toBoolean()
if(toapp){
    apply plugin: 'com.android.application'
    apply plugin: 'therouter'
}else {
    apply plugin: 'com.android.library'
}
apply plugin: 'org.jetbrains.kotlin.android'
apply plugin: 'org.jetbrains.kotlin.kapt'

apply from: '../dependencies.gradle'
android {
    namespace 'com.dudu.demoaudio'
    compileSdk 33

    defaultConfig {
        if(toapp){
            applicationId "com.dudemo.audio"
        }
        minSdk 26
        targetSdk 33

        // 配置文件参数
        // 小部件内部更新配置广播参数
        manifestPlaceholders = [audioWidgetUpdateReceiverAction: "com.dudu.demo.update"]

        buildConfigField("String", "AUDIO_WIDGET_UPDATE", "\"${manifestPlaceholders['audioWidgetUpdateReceiverAction']}\"")

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = '17'
    }
    viewBinding {
        enabled = true
    }
    // 启用 buildConfigField
    buildFeatures {
        buildConfig true
    }
    sourceSets {
        main {
            if (toapp) {
                manifest.srcFile 'src/main/debug/AndroidManifest.xml'
            } else {
                manifest.srcFile 'src/main/release/AndroidManifest.xml'
            }
        }
    }
}

dependencies {
    kapt "cn.therouter:apt:$TheRouterVersion"
    implementation project(path: ':core:datastore-ktx')
    implementation project(path: ':core:common')
    implementation project(path: ':core:audio')
}